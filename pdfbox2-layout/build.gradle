import org.apache.tools.ant.filters.ReplaceTokens

dependencies {
  compile("org.apache.pdfbox:pdfbox:2.0.0")
  testCompile("junit:junit:4.12")
}
  
gradle.projectsEvaluated {
  compileJava.dependsOn(prepareSources)
}

sourceSets {
  main {
    java {
      srcDir 'src/main/java'
    }
    resources {
      srcDir 'src/main/resources'
    }
  }
  test {
    java {
      srcDir 'src/test/java'
      srcDir 'examples'
    }
    resources {
      srcDir 'src/test/resources'
      srcDir 'examples'
      srcDir 'pdfbox2-examples'
    }
  }
}
  
clean{
  delete "src"
  delete "examples"
}  


task prepareSources(description: 'copies and adapts the imports to the given version', dependsOn: ["clean", "copySources", "copyCompatibility", "copyExamples"] ) << {
}

task copySources(type: Copy) {
    from '../src'
    into 'src'
    filter { line -> adaptToPdfBox2(line) }
}

task copyCompatibility(type: Copy, dependsOn: "copySources") {
    from 'compatibility'
    into 'src/main/java'
}

task copyExamples(type: Copy) {
     from('../examples') {
         include '**/*.java'
         filter { line -> adaptToPdfBox2(line) }
     }
     from('../examples') {
         exclude '**/*.java'
     }
    into 'examples'
}

def adaptToPdfBox2(String line) {
   line = line.replaceAll('org.apache.pdfbox.pdmodel.edit.PDPageContentStream', 'org.apache.pdfbox.pdmodel.PDPageContentStream')
   line = line.replaceAll('org.apache.pdfbox.pdmodel.graphics.xobject.PDXObject', 'org.apache.pdfbox.pdmodel.graphics.PDXObject')
   return line
}

